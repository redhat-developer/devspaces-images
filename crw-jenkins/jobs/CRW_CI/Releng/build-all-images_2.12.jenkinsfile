#!/usr/bin/env groovy
import groovy.transform.Field

// PARAMETERS for this pipeline:
//   MIDSTM_BRANCH

// Required due to JENKINS-27421
// from https://stackoverflow.com/questions/42770775/how-to-define-and-iterate-over-map-in-jenkinsfile
@NonCPS
List<List<?>> mapToList(Map map) {
  return map.collect { it ->
    [it.key, it.value]
  }
}

def String nodeLabel = 'rhel83||s390x-rhel8||ppc64le-rhel8' // requires rhpkg, cannot run on x86_64-rhel8
node(nodeLabel) {
    stage ("Trigger builds for whole CRW stack on ${nodeLabel}") {
        wrap([$class: 'TimestamperBuildWrapper']) {
            sh('curl -sSLO https://raw.githubusercontent.com/redhat-developer/codeready-workspaces/' + MIDSTM_BRANCH + '/product/util.groovy')
            def util = load "${WORKSPACE}/util.groovy"
            cleanWs()
            util.installSkopeoFromContainer("")
            util.installYq()
            util.installRhpkg()
            util.installBrewKoji()
            CRW_VERSION = util.getCrwVersion(MIDSTM_BRANCH)
            JOB_BRANCH = util.getJobBranch(MIDSTM_BRANCH)
            currentBuild.description="Jobs run:"
            withCredentials([string(credentialsId:'crw_devstudio-release-token', variable: 'GITHUB_TOKEN'),
                    file(credentialsId: 'crw_crw-build-keytab', variable: 'CRW_KEYTAB')]) {

                def currentJob="" // name of job
                def prevSuccesfulBuildId="" // previous lastSuccesfulBuildId (last one before this started)
                def lastBuildId="" // build in progress
                def currentQuayImage="" // last successful quay tag
                def jenkinsPath="/job/CRW_CI/job/"
                def jenkinsURL="${JENKINS_URL}${jenkinsPath}".replaceAll("//job","/job")

                def List PHASE_LIST = PHASES.tokenize(" ").collect { it.trim() }

                if (PHASE_LIST.contains("1")) {
                    // rebuild lang server artifacts
                    currentJob="crw-deprecated_" + JOB_BRANCH
                    lastBuildId = util.runJob(jenkinsPath + currentJob, true) // DO wait for this to finish, eg., #6
                    currentBuild.description+="<br/>* <a href=${jenkinsPath}${currentJob}/${lastBuildId}>${currentJob}#${lastBuildId}</a>"
                }

                if (PHASE_LIST.contains("2")) {
                    // all the sidecars - start this early since it's 10 builds
                    currentJob="sync-to-downstream_" + JOB_BRANCH
                    lastBuildId = util.runJob(jenkinsPath + currentJob, false) // don't wait for this set of 10 builds to finish
                    currentBuild.description+="<br/>* sidecars: <a href=${jenkinsPath}${currentJob}/${lastBuildId}>${currentJob}#${lastBuildId}</a>"
                }

                currentQuayImageTheia=""
                currentQuayImageTheiaEndpoint=""
                if (PHASE_LIST.contains("3")) {
                    // theia assets (sources) + theia-containers and akamai update too - start early since it's long running
                    currentJob="crw-theia-sources_" + JOB_BRANCH
                    currentQuayImageTheia = util.getLatestImageAndTag("crw/theia-rhel8", "quay")
                    println ("Got currentQuayImageTheia = " + currentQuayImageTheia)
                    currentQuayImageTheiaEndpoint = util.getLatestImageAndTag("crw/theia-endpoint-rhel8", "quay")
                    println ("Got currentQuayImageTheiaEndpoint = " + currentQuayImageTheiaEndpoint)
                    lastBuildId = util.runJob(jenkinsPath + currentJob, false) // don't wait for this to finish
                }

                if (PHASE_LIST.contains("4")) {
                    def jobsList = [
                        "backup":"backup-rhel8",
                        "configbump":"configbump-rhel8",
                        "operator":"crw-2-rhel8-operator",
                        "dashboard": "dashboard-rhel8",
                        "devfileregistry":"devfileregistry-rhel8",
                        "idea":"idea-rhel8",
                        "imagepuller":"imagepuller-rhel8",
                        "jwtproxy":"jwtproxy-rhel8",
                        "machineexec":"machineexec-rhel8",
                        "pluginbroker-artifacts":"pluginbroker-artifacts-rhel8",
                        "pluginbroker-metadata":"pluginbroker-metadata-rhel8",
                        "pluginregistry":"pluginregistry-rhel8",
                        "server":"server-rhel8",
                        "traefik":"traefik-rhel8"
                        ]

                    def Map tasks = [failFast: false]
                    // must define all vars before closure, per https://www.jenkins.io/doc/pipeline/examples/#parallel-from-list
                    for (kv in mapToList(jobsList)) {
                        def key=kv[0] // .key = kv[0]
                        def val=kv[1] // .value = kv[1]
                        def currentJobLoc="crw-" + key + "_" + JOB_BRANCH
                        def currentQuayImageLoc = util.getLatestImageAndTag("crw/" + val, "quay")
                        tasks[currentJobLoc] = { ->
                            println ("Got currentQuayImageLoc (" + val + " / " + key + ") = " + currentQuayImageLoc)
                            def lastBuildIdLoc = " <- util.runJob("+jenkinsPath + currentJobLoc+", true)"
                            lastBuildIdLoc = util.runJob(jenkinsPath + currentJobLoc, true) // DO wait for these to finish
                            println ("Started job for " + key + ": " + jenkinsURL + currentJobLoc + "/" + lastBuildIdLoc)
                            util.waitForNewQuayImage("crw/" + val, currentQuayImageLoc)
                            currentBuild.description+="<br/>* <a href=${jenkinsPath}${currentJobLoc}/${lastBuildIdLoc}>${currentJobLoc}#${lastBuildIdLoc}</a>"
                        }
                    }
                    // wait in parallel
                    parallel(tasks)
                }

                if (PHASE_LIST.contains("3")) {
                    // wait until theia is rebuilt too
                    util.waitForNewQuayImage("crw/theia-rhel8", currentQuayImageTheia)
                    util.waitForNewQuayImage("crw/theia-endpoint-rhel8", currentQuayImageTheiaEndpoint)
                    lastBuildId=getLastBuildResult(jenkinsURL+"crw-theia-endpoint-rhel8_"+JOB_BRANCH)
                    currentBuild.description+="<br/>* <a href=${jenkinsPath}crw-theia-endpoint-rhel8_${JOB_BRANCH}/${lastBuildId}>crw-theia-endpoint-rhel8_${JOB_BRANCH}#${lastBuildId}</a>"
                }

                if (PHASE_LIST.contains("5")) {
                    currentQuayImageMetadata = util.getLatestImageAndTag("crw/crw-2-rhel8-operator-metadata", "quay")
                    println ("Got currentQuayImageMetadata = " + currentQuayImageMetadata)

                    // trigger update-digests job (to rebuild registries and metadata images)
                    currentJob="update-digests-in-metadata_" + JOB_BRANCH
                    lastBuildId = util.runJob(jenkinsPath + currentJob, true) // DO wait for this to finish
                    currentBuild.description+="<br/>* <a href=${jenkinsPath}${currentJob}/${lastBuildId}>update-digests#${lastBuildId}</a>"
                    util.waitForNewQuayImage("crw/crw-2-rhel8-operator-metadata", currentQuayImageMetadata)
                }
            } // withCredentials
        } // wrap
    } // stage
} // node
